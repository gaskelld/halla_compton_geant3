beam_energy=[1]
mass_elec=[2]
pi_number=[3]
hbarc=[4]
lambda=[5]
thetabend = [6] 
zdrift = [7] 
mfield = [8]
lmag = [9]
ldet=[10]
 
ve/cr eb(1) r [beam_energy]
ve/cr me(1) r [mass_elec]
ve/cr pi(1) r [pi_number]
ve/cr hbarc(1) r [hbarc]
ve/cr lambda(1) r [lambda]
ve/cr thbend(1) r [thetabend]
ve/cr zdrift(1) r [zdrift]
ve/cr mfield(1) r [mfield]
ve/cr lmag(1) r [lmag]
ve/cr ldet(1) r [ldet]


*->sh rm distance_to_rho_mfield-[mfield].table
FORTRAN/FILE 3 distance_to_rho_mfield-[mfield].table


mess
mess ##### Compton Theory - Maximum Compton Distance #####
mess
mess ##### EU: one plane, no tilt ########################
mess
mess BEAM ENERGY =	[beam_energy]
mess ELECTRON MASS =	[mass_elec]
mess PI NUMBER =	[pi_number]
mess HBARC =		[hbarc]
mess LAMBDA =		[lambda]
mess THETA BEND =	[thetabend]
mess Z DRIFT =		[zdrift]
mess MAG FIELD =	[mfield]
mess L MAGNET =		[lmag]
mess L DET =		[ldet]

*        sigma const=hbarc*1.0E-15/(2*5.788381E-14*me)
	sigma const=10/3
	sigma thbend=thbend*pi/180. 
	sigma alpha=1.0/137.0
	sigma re=alpha*hbarc/me

* incident electron (electron gamma-factor (dimensionless))
        sigma gamma=eb/me

* incident photon  photon energy (GeV)
        sigma k=2*pi*hbarc/(lambda*1e6)

* undeflected beam height 
	sigma p_beam=sqrt(eb*eb-me*me)
	sigma r=const*p_beam/mfield
	sigma theta=asin(lmag/r)
	sigma h=r*(1-cos(theta))
	sigma y0=h+ldet*tan(theta)

*outgoing photon// max recoil photon energy (GeV)
        sigma a=1/(1+4*k*gamma/me) 
        sigma kmax=4*a*k*gamma*gamma 
	ve/copy kmax kkk
	
      do i=1,1000	
	sigma p_edge=p_beam-kkk         
	sigma r_edge=const*p_edge/mfield
        sigma th_edge=asin(lmag/r_edge)
	sigma hprime=r_edge*(1-cos(th_edge))
        sigma yprime=hprime+ldet*tan(th_edge)
 	sigma maxdist=yprime-y0

	sigma rho=kkk/kmax
	md=maxdist(1)
	r=rho(1)
	 if ([r] > 0 .OR. [md] > 0) then
*	  mess [i] [md] [r]
	  ve/write maxdist,rho distance_to_rho_mfield-[mfield].table (F9.6,1x,F9.6) ' '
	 endif
	sigma kkk=kkk-0.00005
	ve/del p_edge; ve/del r_edge; ve/del th_edge; ve/del hprime
	ve/del yprime; ve/del maxdist; ve/del rho
      enddo
  	
        ve/del eb; ve/del me; ve/del pi; ve/del hbarc; ve/del lambda
	ve/del zdrift; ve/del mfield; ve/del lmag; ve/del ldet; ve/del thbend
	ve/del alpha; ve/del re; ve/del gamma; ve/del y0; ve/del a; ve/del kmax
	ve/del p_beam; ve/del r; ve/del h; ve/del theta

      FORTRAN/CLOSE 3
